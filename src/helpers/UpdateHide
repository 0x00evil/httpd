:
eval 'exec perl -S $0 ${1+"$@"}'
    if $running_under_some_shell;
## ====================================================================
## Copyright (c) 1995-1998 The Apache Group.  All rights reserved.
##
## Redistribution and use in source and binary forms, with or without
## modification, are permitted provided that the following conditions
## are met:
##
## 1. Redistributions of source code must retain the above copyright
##    notice, this list of conditions and the following disclaimer. 
##
## 2. Redistributions in binary form must reproduce the above copyright
##    notice, this list of conditions and the following disclaimer in
##    the documentation and/or other materials provided with the
##    distribution.
##
## 3. All advertising materials mentioning features or use of this
##    software must display the following acknowledgment:
##    "This product includes software developed by the Apache Group
##    for use in the Apache HTTP server project (http://www.apache.org/)."
##
## 4. The names "Apache Server" and "Apache Group" must not be used to
##    endorse or promote products derived from this software without
##    prior written permission. For written permission, please contact
##    apache@apache.org.
##
## 5. Redistributions of any form whatsoever must retain the following
##    acknowledgment:
##    "This product includes software developed by the Apache Group
##    for use in the Apache HTTP server project (http://www.apache.org/)."
##
## THIS SOFTWARE IS PROVIDED BY THE APACHE GROUP ``AS IS'' AND ANY
## EXPRESSED OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
## IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
## PURPOSE ARE DISCLAIMED.  IN NO EVENT SHALL THE APACHE GROUP OR
## ITS CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
## SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
## NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
## LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
## HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
## STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
## ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED
## OF THE POSSIBILITY OF SUCH DAMAGE.
## ====================================================================
##
## This software consists of voluntary contributions made by many
## individuals on behalf of the Apache Group and was originally based
## on public domain software written at the National Center for
## Supercomputing Applications, University of Illinois, Urbana-Champaign.
## For more information on the Apache Group and the Apache HTTP server
## project, please see <http://www.apache.org/>.
##
##

##
##  UpdateHide -- update the include/hide.h header file
##  Written by Ralf S. Engelschall, <rse@apache.org>
##

#   usage
if ($#ARGV != 2) {
    print STDERR "Usage:   UpdateHide <httpd-binary> <hide-header-file> <gnu-triple>\n";
    print STDERR "Example: UpdateHide httpd include/hide.h `helpers/GuessOS`\n";
    print STDERR "Hint:    Use the 'makeupdate' target of src/Makefile to run it\n";
    exit(1);
}

#   the parameters
$httpdbinary = $ARGV[0];
$headerfile  = $ARGV[1];
$gnutriple   = $ARGV[2];

#   configuration
$listbegin  = '\n#ifdef HIDE\n';
$listend    = '\n#endif /* HIDE */\n';
@excluded   = qw(
    main errno environ optarg
    dlclose dlerror dlopen dlsym
    start end atexit exit etext edata
    regcomp regerror regexec regfree
    aplog_error
);
@included   = qw(
    D:db_auth_module
    D:dbm_auth_module
    T:find_pool
    T:pool_is_ancestor
    T:pool_join
    T:check_alarm
);

#   determine os-dependend stuff
if ($gnutriple =~ m|.*-freebsd2.*|) {
    $nm_cmd   = '/usr/bin/nm -g';
    $us_strip = 1;
}
elsif ($gnutriple =~ m|.*-solaris2.*|) {
    $nm_cmd   = '/usr/ccs/bin/nm -p -g';
    $us_strip = 0;
}
elsif ($gnutriple =~ m|.*-hpux.*|) {
    $nm_cmd   = '/usr/ccs/bin/nm -p -g';
    $us_strip = 0;
}
elsif ($gnutriple =~ m|.*-linux.*|) {
    $nm_cmd   = '/usr/bin/nm -g';
    $us_strip = 0;
}
else {
    print STDERR "Sorry, UpdateHide currently can be used only under\n";
    print STDERR "the following OS: FreeBSD, Solaris, HPUX, Linux\n";
    exit(1);
}

#   read current file as template
open(FP, "<$headerfile");
$header = '';
while (<FP>) {
    $header .= $_;
}
close(FP);

#   determine list of exported symbols
%SB = ();
%SD = ();
%ST = ();
@L = `$nm_cmd $httpdbinary`;
foreach $l (@L) {
    if ($l =~ m|^.*\s+([BDRT])\s+(\S+)\s*$|) {
        ($type, $name) = ($1, $2);

        #   remove leading underscore (C linker convention)
        $name =~ s|^_|| if $us_strip;
        #   remove perhaps already existing AP_ prefix
        #   (when running this script while httpd was
        #    already compiled with HIDE defined)
        $name =~ s|^AP_||;  
        #   skip any already-protected symbols
        next if ($name =~ m/^ap(?:_|api)/);
        #   skip any compiler- or system-private symbols
        next if ($name !~ m|^[a-zA-Z]+|);
        #   skip any symbols in our exclusion list
        next if (grep(/^$name$/, @excluded));

        #   insert the symbol into our lists
        $SB{$name}++ if $type eq 'B';
        $SD{$name}++ if $type eq 'D' or $type eq 'R';
        $ST{$name}++ if $type eq 'T';
    }
}
#   additionally add entries from static include list
foreach $i (@included) {
    ($type, $name) = ($i =~ m|^([BDT]):(.+)$|);
    $SB{$name}++ if $type eq 'B';
    $SD{$name}++ if $type eq 'D';
    $ST{$name}++ if $type eq 'T';
}

#   generate new redefinition list
sub mksublist {
    my ($name, %SL) = @_;
    my ($o, $s);

    $o .= "\n" .
          "/*\n" .
          " *  $name segment symbols\n" .
          " */\n";
    foreach $s (sort(keys(%SL))) {
        $o .= sprintf("#define %-30s AP_%s\n", $s, $s);
    }
    return $o;
}
$list = '';
$list .= &mksublist("BSS",  %SB);
$list .= &mksublist("Data", %SD);
$list .= &mksublist("Text", %ST);

#   replace old redefinition list with new one
$header =~ s|($listbegin).*?($listend)|$1$list$2|s;

#   write new header file
open(FP, ">$headerfile");
print FP $header;
close(FP);

##EOF##
